// Тип данных строка
// Автор: Гордеев Никита, 2024

Процедура ПриНачалеРаботыСистемы()
	
	// =========================== инициализировать строку ===========================
	ПеременнаяСтрока = "Пример текста";  
	Сообщить("Строка: " + ПеременнаяСтрока);

	МногострочныйТекст = "123
	|345";
	Сообщить("МногострочныйТекст: " + МногострочныйТекст);

	СтрокаПредупреждение = "Внимание! "+"Параметры введены некорректно"; 
	Сообщить("СтрокаПредупреждение: " + СтрокаПредупреждение);

	// =========================== операции над строками ===========================

	// длина строки
	Сообщить(СтрДлина("123456789")); // 9

	// N символов слева
	Строка = "Иван Грозный";
    Сообщить(Лев(Строка, 4)); // Иван

	// N символов справа
	Строка = "Иван Грозный";
	Сообщить(Прав(Строка, 7)); // Грозный

	// как взять строку из середины
    Строка = "Мама, папа, я.";
    Сообщить(Сред(Строка, 7, 4)); // папа
	// строки индексируются с единицы
	Сообщить(Сред(Строка, -1, 4)); // Мама
	Сообщить(Сред(Строка, 0, 4)); // Мама
	Сообщить(Сред(Строка, 1, 4)); // Мама
	
	// найти подстроку
	Строка = "Привет, мир!";
	Сообщить(СтрНайти(Строка, "мир")); // 9, строки индексируются с единицы

	// число вхождений подстроки в строку 
	Сообщить(СтрЧислоВхождений("я, я и ещё раз я.", "я")); // 3

	// заменить подстроку в строке
	Сообщить(СтрЗаменить("Идёт сильный дождь.", "дождь", "снег"));

	// проверить на пустую строку
	Если ПустаяСтрока("         ") Тогда
        Сообщить("Строка не содержит значащих символов.");
    КонецЕсли;

	// удалить незначащие символы из строки
	Сообщить(СокрЛ("   из лесу ёлочку    ")); // "из лесу ёлочку    "
	Сообщить(СокрП("   из лесу ёлочку    ")); // "   из лесу ёлочку"
	Сообщить(СокрЛП("   из лесу ёлочку    ")); // "из лесу ёлочку"

	// вставить перенос строки
    Строка = "Первая строка" + Символы.ПС + "Вторая строка" + Символы.ПС + "Третья строка";

	// получение значений из многострочных строк
	Сообщить(СтрЧислоСтрок(Строка)); // 3
    Сообщить(СтрПолучитьСтроку(Строка, 2)); // "Вторая строка"

	// Верхний, нижний и титульный регистры
	Сообщить(ВРег("россия - родина моя!")); // РОССИЯ - РОДИНА МОЯ!
    Сообщить(НРег("МИР, ТРУД, МАЙ!")); // мир, труд, май
    Сообщить(ТРег("мама мыла раму")); // Мама Мыла Раму

	// получить код символа строки
	Сообщить(КодСимвола("A")); // 65
    Сообщить(Символ(65)); // "A"

	// код третьего символа в строке
    Сообщить(КодСимвола("ABC", 3)); //67

	// код первого символа в строке
	КодПервогоСимволаФамилии = КодСимвола("Грозный");
	Сообщить(КодПервогоСимволаФамилии); // 1043, потому что Unicode.

	// преобразовать строку в число
	Сообщить(Число("125.36")); // 125,36
    Сообщить(Число("125,36")); // 125,36

	// разбить строку с разделителями
    Строки = СтрЗаменить("январь, февраль, март", ", ", Символы.ПС);
    Для Индекс = 1 По СтрЧислоСтрок(Строки) Цикл
        Сообщить(СтрПолучитьСтроку(Строки, Индекс));
	КонецЦикла;

	// вычислить максимальное из нескольких значений
	Сообщить(Макс("aaa", "bbb", "ccc")); // ccc

	// вычислить минимальное из нескольких значений
	Сообщить(Мин("aaa", "bbb", "ccc")); // aaa
	
КонецПроцедуры