// 135. Конфеты

// В ряд стоят n детей. Каждому ребёнку присвоен рейтинг, указанный в целочисленном массиве ratings.

// Вы раздаёте конфеты этим детям, соблюдая следующие требования:

// У каждого ребёнка должна быть хотя бы одна конфета.
// Дети с более высоким рейтингом получают больше конфет, чем их соседи.
// Верните минимальное количество конфет, которое должно быть у вас, чтобы раздать их детям.

// Пример 1:
// Входные данные: оценки = [1,0,2]
// [1,1,1]
// [2,1,2]
// Выходные данные: 5
// Пояснение: Вы можете выделить первому, второму и третьему ребенку по 2, 1, 2 конфеты соответственно.

// Пример 2:
// Входные данные: рейтинги = [1,2,2]
// [1,1,1]
// [1,2,1]
// Результат: 4
// Объяснение: вы можете дать первому, второму и третьему ребёнку по 1, 2 и 1 конфете соответственно.
// Третий ребёнок получает 1 конфету, потому что он удовлетворяет двум вышеуказанным условиям.
 
// Ограничения:
// n == ratings.length
// 1 <= n <= 2 * 10^4
// 0 <= ratings[i] <= 2 * 10^4

Функция РаспределитьКонфеты(Рейтинги) 

    КоличествоДетей = Рейтинги.Количество(); // Количество детей
    Конфеты = Новый Массив(КоличествоДетей); // Массив для хранения количества конфет

    // Каждый ребенок получает хотя бы одну конфету
    Для Индекс = 0 По КоличествоДетей - 1 Цикл
        Конфеты[Индекс] = 1;
    КонецЦикла;

    // Первый проход: слева направо
    Для Индекс = 1 По КоличествоДетей - 1 Цикл
        Если Рейтинги[Индекс] > Рейтинги[Индекс - 1] Тогда
            Конфеты[Индекс] = Конфеты[Индекс - 1] + 1;
        КонецЕсли;
    КонецЦикла;

    // Второй проход: справа налево
    Для Индекс = 0 По КоличествоДетей - 2 Цикл
		ОбратныйИндекс = КоличествоДетей - 2 - Индекс;
        Если Рейтинги[ОбратныйИндекс] > Рейтинги[ОбратныйИндекс + 1] Тогда
            Конфеты[ОбратныйИндекс] = Макс(Конфеты[ОбратныйИндекс], Конфеты[ОбратныйИндекс + 1] + 1);
        КонецЕсли;
    КонецЦикла;

    // Суммируем количество конфет
    ОбщееКоличествоКонфет = 0;
    Для Индекс = 0 По Конфеты.Количество() - 1 Цикл
        ОбщееКоличествоКонфет = ОбщееКоличествоКонфет + Конфеты[Индекс];
    КонецЦикла;

    Возврат ОбщееКоличествоКонфет;

КонецФункции

Процедура ПриНачалеРаботыСистемы()
    // Пример 1
    Массив1 = Новый Массив;
	Массив1 = СтрРазделить("1,0,2", ",");
    Результат1 = РаспределитьКонфеты(Массив1);
	Сообщить("Общее количество конфет 1: " +  Результат1);

	// Пример 2
	Массив2 = Новый Массив;
	Массив2 = СтрРазделить("1,2,2", ",");
	Результат2 = РаспределитьКонфеты(Массив2);
	Сообщить("Общее количество конфет 2: " +  Результат2);
КонецПроцедуры