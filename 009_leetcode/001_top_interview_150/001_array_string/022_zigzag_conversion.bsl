// 6. Преобразование зигзагообразной строки
		
// Строка «КОСМОСЗОВЕТНАС» записана зигзагообразным узором в заданном количестве строк следующим образом: 
// (вы можете отобразить этот узор фиксированным шрифтом для лучшей читаемости)

// К   О   В   А
// О М С О Е Н С
// С   З   Т

// А затем прочитать построчно: «КОВАОМСОЕНССЗТ»

// Напишите код, который будет принимать строку и выполнять это преобразование с учётом количества строк:
// string convert(string s, int numRows);
 
// Пример 1:
// Входные данные: s = "КОСМОСЗОВЕТНАС", numRows = 3
// Вывод: "КОВАОМСОЕНССЗТ"

// Пример 2:
// Входные данные: s = "КОСМОСЗОВЕТНАС", numRows = 4
// Вывод: "КЗАОСОНССОВТМЕ"
// Объяснение:
// К     З    А
// О   С О  Н С
// С О   В Т
// М     Е

// Пример 3:
// Входные данные: s = "А", numRows = 1
// Вывод: "А"

// Ограничения:
// 1 <= s.length <= 1000
// s состоит из английских букв (строчных и прописных), знаков «,» и «.».
// 1 <= numRows <= 1000

// 6. Zigzag Conversion // LeetCode URL: https://leetcode.com/problems/zigzag-conversion/?envType=study-plan-v2&envId=top-interview-150

Функция ЗигзагообразнаяСтрока(Строка, КоличествоСтрок) 
    // Если строка помещается в одну строку возвращаем её
    Если КоличествоСтрок = 1 ИЛИ СтрДлина(Строка) <= КоличествоСтрок Тогда
        Возврат Строка;
    КонецЕсли;

    // Создаем массив строк для хранения символов каждой строки загзагообразного узора
    МассивСтрок = Новый Массив;
    Для Индекс = 0 По КоличествоСтрок - 1 Цикл
        МассивСтрок.Добавить("");
    КонецЦикла;

    ТекущаяСтрока = 0; // Начинаем с первой строки
    НаправлениеВниз = Ложь; // Флаг направления: Ложь — движение вверх, Истина — движение вниз

    // Проходим по каждому символу исходной строки и распределяем их по строкам зигзага
    Для Индекс = 0 По СтрДлина(Строка) - 1 Цикл
		// Получаем текущий символ, добавляем в текущую строку
        Символ = Сред(Строка, Индекс + 1, 1); 
        МассивСтрок[ТекущаяСтрока] = МассивСтрок[ТекущаяСтрока] + Символ;
        
		// Достигли верхней или нижней границы, меняем направление
		Если ТекущаяСтрока = 0 ИЛИ ТекущаяСтрока = КоличествоСтрок - 1 Тогда
            НаправлениеВниз = Не НаправлениеВниз; 
        КонецЕсли;

		// Переходим в следующую или предыдущую строку в зависимости от направления
        ТекущаяСтрока = ТекущаяСтрока + ?(НаправлениеВниз, 1, -1); 
    КонецЦикла;

    // Объединяем все строки
    Результат = "";
    Для Индекс = 0 По КоличествоСтрок - 1 Цикл
        Результат = Результат + МассивСтрок[Индекс];
    КонецЦикла;

	// Возвращаем итоговую строку
    Возврат Результат; 
КонецФункции

Процедура ПриНачалеРаботыСистемы()
    // Пример 1
    Строка1 = "КОСМОСЗОВЕТНАС";
    КоличествоСтрок1 = 3;
    Сообщить("Результат для примера 1: " + ЗигзагообразнаяСтрока(Строка1, КоличествоСтрок1));

    // Пример 2
    Строка2 = "КОСМОСЗОВЕТНАС";
    КоличествоСтрок2 = 4;
    Сообщить("Результат для примера 2: " + ЗигзагообразнаяСтрока(Строка2, КоличествоСтрок2));

    // Пример 3
    Строка3 = "A";
    КоличествоСтрок3 = 1;
    Сообщить("Результат для примера 3: " + ЗигзагообразнаяСтрока(Строка3, КоличествоСтрок3));
КонецПроцедуры